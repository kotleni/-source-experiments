global proc vstGetChosen(string $windowName)
{
	string $chosen[];
	$chosen = `textScrollList -q -si chooserUiTextList`;
	deleteUI $windowName;
	vstPuppetNulls -c $chosen[0];
}

global proc vstAutoPuppet()
{
	// get the currently supported creature types from the script template directory
	string $templateDir = `vstGetTemplateDir`;
	string $dirList[] = `getFileList -fld $templateDir -fs "locators_*.mel"`;
	string $characterList[];

	for ( $d in $dirList )
	{
		string $buffer[];
		tokenize $d  "_."  $buffer;
		$characterList[size( $characterList )] = $buffer[1];
	}

	string $windowName = "vstAutoPuppet";
	if ( `window -ex $windowName` )
	{
		deleteUI $windowName;
	}

	window -h 240 -w 175 -s 0 $windowName;
	frameLayout -borderVisible false -labelVisible false;
	columnLayout -adj 1;
	textScrollList -dcc ( "vstGetChosen " + $windowName ) -height 185  -numberOfRows 8 -allowMultiSelection 0 chooserUiTextList;

	for ($c in $characterList)
	{
		textScrollList -e -append $c chooserUiTextList;
	}

	// select an item so if the "Choose Puppet" button is pressed before the user makes a selection it won't error
	// remember that the textScrollList uses a 1-based index
	textScrollList -e -sii 1 chooserUiTextList; 
	button -label "Choose Puppet" -ann "choose items" -c ( "vstGetChosen " + $windowName ) chooseUiButton;
	setParent ..;
	showWindow $windowName;
}
